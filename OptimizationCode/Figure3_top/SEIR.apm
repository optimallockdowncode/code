File ipopt.opt
	max_iter 10000 
	tol 1.000000e-08 
End File

Parameters
	final
	tbar = 0.000000 , >=0 <= 1
	tsar = 0.000000 , >=0 <= 1
	p = 0.000000 , >= 0 <= 1
	pq = 0.000000 , >= 0 <= 1

Constants
	N = 1.000000
	eps = 0.030000
	beta = 0.204000
	sig = 0.150000
	gamma = 0.150000
	theta = 0.250000
	tau = 0.000000
	rho = 0.700000
	kappa = 0.007500
	c1 = 0.001000
	c2 = 0.000100
	vsl = 40.000000
	b1 = 0.010000
	b2 = 13.500000
	b3 = 0.050000

Variables
	S = 0.970000 , >= 0 <= N
	Sq = 0.000000 , >= 0 <= N
	E = 0.015000 , >= 0 <= N
	Eq = 0.000000 , >= 0 <= N
	IA = 0.015000 , >= 0 <= N
	IAq = 0.000000 , >= 0 <= N
	IS = 0.000000 , >= 0 <= N
	ISq = 0.000000 , >= 0 <= N
	J = 0.000000 , >= 0 <= N
	Jq = 0.000000 , >= 0 <= N
	D = 0.000000 , >= 0 <= N
	RA = 0.000000 , >= 0 <= N
	RAq = 0.000000 , >= 0 <= N
	RS = 0.000000 , >= 0 <= N
	RSq = 0.000000 , >= 0 <= N
	t = 0.000000
	Production = 0.000000

Intermediates
	lam = beta * (E + IA + theta * (Eq + IAq))/(S + E + IA + RA + RS + theta * (Sq + Eq + IAq + RAq + RSq))
	Ts1 = (S+E+IA+RA)*tbar
	Ts2 = (Sq+Eq+IAq+RAq)*tsar
	Ts = Ts1+Ts2
	LTs= (Ts)*15/200
	N1 = S + E + IA + IS + J + RA + RS
	Nq = Sq + Eq + IAq + ISq + Jq + RAq + RSq
	Wf = N1-(1-rho)*J-IS+rho*(Nq - ISq) - LTs
	drate = 0.01*gamma + kappa*(IS+ISq)

Equations
	maximize (final * Production)

	$t = 1
	$S  = -lam * S + tau * (RA + RS) - p * S + pq * Sq
	$Sq = -theta * lam * Sq + tau * (RAq + RSq) + p * S - pq * Sq
	$E  = lam * S - (sig + tbar*0.5) * E - p * E + pq * Eq
	$Eq = theta * lam * Sq - (sig + tsar*0.5 ) * Eq + p * E - pq * Eq
	$IA = 0.5 * sig * E  - (gamma + tbar*0.5 ) * IA  - p * IA + pq * IAq
	$IAq= 0.5 * sig * Eq - (gamma + tsar*0.5 ) * IAq + p * IA - pq * IAq
	$IS = 0.5 * sig * E - (gamma + drate) * IS - p * IS + pq * ISq
	$ISq= 0.5 * sig * Eq - (gamma + drate) * ISq + p * IS - pq * ISq
	$J  = tbar* 0.5 * (IA + E) - gamma * J - p * J + pq * Jq
	$Jq = tsar*0.5*(IAq + Eq) - gamma * Jq + p * J - pq * Jq
	$D = drate*(IS + ISq)
	$RA = gamma * IA - tau * RA - p * RA + pq * RAq
	$RAq = gamma * IAq - tau * RAq + p * RA - pq * RAq
	$RS  = gamma * (IS + J) - tau * RS - p * RS + pq * RSq
	$RSq = gamma * (ISq + Jq) - tau * RSq + p * RS - pq * RSq
	$Production  = Wf/(365*N) - drate*(IS + ISq)*vsl/N - c1*(p^2+pq^2)-c2*((tbar+tsar)^2)
	Nq <= 0.7*N
	Ts <=b1/(1+b2*exp(-b3*t))
